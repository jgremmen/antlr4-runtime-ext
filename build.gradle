plugins {
  id 'java-library'
  id 'idea'
  id 'antlr'
  id 'maven-publish'
  id 'jacoco'
  id 'signing'
}


layout.buildDirectory = ".build"


description 'ANTLR4 Runtime Extensions'
group 'de.sayayi.lib'
version '0.5.1-SNAPSHOT'


def antlr4Version = "4.13.2"
def junitVersion = "5.11.+"
def lombokVersion = "1.18.+"
def jetbrainsAnnotationsVersion = "25.0.+"


configurations {
  antlr4 {
    visible = false;
  }

  api.setExtendsFrom([])
}


repositories {
  mavenCentral()
}


java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}


test {
  useJUnitPlatform()
}


apply from: file('gradle/javadoc.gradle')
apply from: file('gradle/publishing.gradle')


dependencies {
  // java
  api "org.antlr:antlr4-runtime:${antlr4Version}"

  compileOnly "org.jetbrains:annotations:${jetbrainsAnnotationsVersion}"

  // test
  testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

  testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
  testCompileOnly "org.jetbrains:annotations:${jetbrainsAnnotationsVersion}"

  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher"

  antlr4 "org.antlr:antlr4:${antlr4Version}"
}


generateGrammarSource {
  antlrClasspath = configurations.antlr4
}
